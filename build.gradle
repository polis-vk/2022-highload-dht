plugins {
    id 'java'
    id 'application'
    id 'net.ltgt.errorprone' version '2.0.2'
    id 'checkstyle'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    // Checks
    errorprone 'com.google.errorprone:error_prone_core:2.15.0'
    checkstyle 'com.puppycrawl.tools:checkstyle:10.3.3'

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.1'
    implementation 'ch.qos.logback:logback-classic:1.4.1'

    // Annotations for better code documentation
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // Our beloved one-nio
    implementation 'ru.odnoklassniki:one-nio:1.5.0'

    //implementation 'jdk.incubator.foreign'

    // JUnit Jupiter test framework
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

application {
    mainClass = 'ok.dht.Server'
    applicationDefaultJvmArgs = ['-Xmx128m']
}

test {
    maxHeapSize = "128m"
    useJUnitPlatform()
}

checkstyle {
    configFile = new File("checkstyle.xml")
    checkstyleTest.enabled = false
}

// Use JDK Incubator Foreign module (for MemorySegment users)
def incubatorArgs = ["--add-modules", "jdk.incubator.foreign"]

tasks.withType(JavaCompile) {
    options.compilerArgs += incubatorArgs
}

tasks.withType(Test) {
    jvmArgs += incubatorArgs
}
